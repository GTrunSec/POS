:PROPERTIES:
:ID:       55e5de92-922e-4e91-aa8c-c8121545aac8
:header-args: :noweb tangle :comments noweb :exports both
:PRJ-DIR: ../../units/nixos/nixosProfiles
:END:
#+TITLE: nixosProfiles
#+AUTHOR: GuangTao Zhang
#+EMAIL: gtrunsec@hardenedlinux.org
#+DATE: 2023-10-08 Sun 22:00

- tags :: [[id:50ab7f73-4fae-481b-8987-6e206a9d8f95][homeProfiles]]


* Suites

** cloud

#+begin_src nix :tangle (concat (org-entry-get nil "PRJ-DIR" t) "/cloud.nix")
{
  root,
  omnibus,
  POP,
  flops,
  lib,
}:
let
  inherit (lib.omnibus) mkSuites;
  srvosCustom =
    (omnibus.pops.srvos.addExporters [
      (POP.extendPop flops.haumea.pops.exporter (
        self: _super: {
          exports.srvosCustom = self.outputs [
            {
              value = {selfModule}: removeAttrs selfModule ["imports"];
              path = [
                "common"
                "default"
              ];
            }
            {
              value = {selfModule}: removeAttrs selfModule ["imports"];
              path = [
                "server"
                "default"
              ];
            }
          ];
        }
      ))
    ]).exports.srvosCustom;

  presets = root.presets;
in
with presets;
mkSuites {
  default = [
    {
      keywords = [
        "srvos"
        "server"
        "presets"
        "init"
      ];
      knowledges = ["https://github.com/nix-community/srvos"];
      profiles = [
        nix
        openssh
        srvosCustom.server.default
        srvosCustom.common.default
        srvosCustom.common.serial
        srvosCustom.common.sudo
        srvosCustom.common.upgrade-diff
        srvosCustom.mixins.nix-experimental
        (
          {pkgs, lib, ...}:
          {
            boot.kernelPackages = lib.mkDefault pkgs.linuxPackages_latest;
            boot.tmp.cleanOnBoot = true;
            documentation.enable = false;
          }
        )
      ];
    }
    {
      keywords = [
        "zswap"
        "memory"
        "optimization"
        "compression"
      ];
      knowledges = ["https://wiki.archlinux.org/title/zswap"];
      profiles = [zswap];
    }
    {
      keywords = [
        "allocators"
        "memory"
        "optimization"
      ];
      knowledges = ["https://github.com/microsoft/mimalloc#performance"];
      profiles =
        [
          # mimalloc
        ];
    }
  ];

  btrfs = [
    fileSystems.btrfs
    fileSystems.disko-btrfs
    fileSystems.impermanence
  ];

  contabo = [
    self.default
    cloud.contabo
    {
      keywords = [
        "disko"
        "boot"
      ];
      knowledges = [""];
      profiles = [
        self.btrfs
        {
          boot.loader.grub.device = "";
          disko.devices.__profiles__ = {
            boot = true;
            name = "sda";
            device = "/dev/sda";
          };
        }
      ];
    }
  ];

  amazon = [
    {
      keywords = ["amazon"];
      knowledges = ["https://github.com/nix-community/srvos/blob/main/nixos/hardware/amazon/default.nix"];
      profiles = [srvosCustom.hardware.amazon.default];
    }
  ];
}
#+end_src



** coding

#+begin_src nix :tangle (concat (org-entry-get nil "PRJ-DIR" t) "/dev.nix")
{ root, self }:
let
  presets = root.presets;
in
with presets; {
  minimal = [ shell.default ];

  default = [
    self.minimal
    self.mathematic
    coding.nickel
    coding.conf
    coding.bash
    coding.d2
    coding.nix
  ];

  coding = [
    self.default
    {
      config.omnibus.coding = {
        bash.lsp = true;
        nickel.lsp = true;
        typst.lsp = true;
        conf.lsp = true;
      };
    }
  ];

  mathematic = [ coding.typst ];
}
#+end_src

** hardware


#+begin_src nix :tangle (concat (org-entry-get nil "PRJ-DIR" t) "/hardware.nix")
{ root, self }:
let
  presets = root.presets;
in
with presets; {
  default = [
    audio.bluetooth
    audio.pipewire
  ];
}
#+end_src
