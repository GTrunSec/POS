:PROPERTIES:
:ID:       f7adb0ad-2cc1-4723-a796-bf608682456a
:header-args: :noweb tangle :comments noweb :exports both
:PRJ-DIR: ../..
:EXP-DIR: ../../examples
:CODE-DIR: ../../src/pops
:END:
#+TITLE: pops.packages
#+AUTHOR: GuangTao Zhang
#+EMAIL: gtrunsec@hardenedlinux.org
#+DATE: 2023-11-19 Sun 16:25


function: ~omnibus.pops.packages~ is responsible for loading packages in a tree structure then nixpkgs.callPackage with *path* automatically.

- code

#+begin_src nix :tangle (concat (org-entry-get nil "CODE-DIR" t) "/packages.nix")
{
  super,
  root,
  POP,
  flops,
  lib,
}:
load:
let
  inherit (flops) recursiveMerge';
in
(super.load (
  recursiveMerge' [
    {
      loader =
        __inputs__: path:
        #  without the scope loader
        (__inputs__.inputs.nixpkgs.extend (_: _: { inherit __inputs__; })).callPackage
          path
          { };
      transformer = [ (_cursor: dir: if dir ? default then dir.default else dir) ];
    }
    load
  ]
)).addExporters
  [
    (POP.extendPop flops.haumea.pops.exporter (
      self: _super: {
        exports =
          let
            inherit
              (root.errors.requiredInputsLazily self.layouts.self.load.inputs.inputs
                "omnibus.pops.packages"
                [ "nixpkgs" ]
              )
              nixpkgs
              ;
            inherit (nixpkgs) newScope;
            inherit (nixpkgs.lib) makeScope;
          in
          {
            derivations =
              lib.attrsets.filterDerivations self.exports.packages
              // (lib.optionalAttrs (self.exports.packages ? by-loader)
                lib.attrsets.filterDerivations
                (lib.concatMapAttrs (_: v: v) self.exports.packages.by-loader)
              );

            scopePackagesPop =
              selfScope:
              (self.layouts.self.addLoadExtender {
                load = {
                  loader =
                    __inputs__: path:
                    (selfScope.overrideScope (_: _: { inherit __inputs__; })).callPackage path { };
                  inputs = { };
                };
              });

            packages =
              (makeScope newScope (
                selfScope: (self.exports.scopePackagesPop selfScope).exports.default
              )).overrideScope
                (
                  selfScope: _:
                  let
                    checkPath =
                      pathSuffix:
                      if lib.pathExists (self.layouts.self.load.src + pathSuffix) then
                        true
                      else
                        false;
                  in
                  {
                    by-loader = lib.optionalAttrs (checkPath "/by-loader/python3Packages") {
                      python3Packages =
                        selfScope.callPackage lib.omnibus.mkPython3PackagesWithScope
                          { };
                    };
                    __inputs__ = {
                      __load__ = self.layouts.self.load;
                      callPackagesWithLoader =
                        selfScope: src:
                        assert lib.assertMsg (!(lib.readDir src) ? "default.nix") ''
                          The top-level of ${src} must not contain a file named "default.nix"
                        '';
                        (super.load {
                          loader =
                            _: path:
                            (selfScope.overrideScope (
                              _: _: { __inputs__ = self.layouts.self.load.inputs; }
                            )).callPackage
                              path
                              { };
                          inherit src;
                          transformer = [ (_cursor: dir: if dir ? default then dir.default else dir) ];
                        }).exports.default;
                    };
                  }
                );

            overlays = {
              default =
                final: _prev:
                (self.exports.packages.packages (
                  final // { overrideScope = self.exports.packages.overrideScope; }
                ));
              composedPackages =
                final: prev:
                (
                  (self.exports.packages.overrideScope (
                    _: scopeSuper: {
                      python3 = prev.python3.override (
                        old: {
                          packageOverrides =
                            prev.lib.composeExtensions (old.packageOverrides or (_: _: { }))
                              (
                                pythonSelf: _:
                                if (scopeSuper ? by-loader && scopeSuper.by-loader ? python3Packages) then
                                  scopeSuper.by-loader.python3Packages.packages pythonSelf
                                else
                                  { }
                              );
                        }
                      );
                      python3Packages = prev.python3Packages.override (
                        old: {
                          overrides = prev.lib.composeExtensions (old.overrides or (_: _: { })) (
                            pythonSelf: _:
                            if (scopeSuper ? by-loader && scopeSuper.by-loader ? python3Packages) then
                              scopeSuper.by-loader.python3Packages.packages pythonSelf
                            else
                              { }
                          );
                        }
                      );
                    }
                  )).packages
                  (final // { overrideScope = self.exports.packages.overrideScope; })
                );
            };
          };
      }
    ))
  ]
#+end_src

- *pops.packages* have an arg named by anti-namespace of *__inputs__* in the scopePackages, that *__inptus__* can pass all args same as haumea.inputs, such as {super,self, ...} => {__inputs__.self, __inputs__.super, inputs.{...} }

- callPythonPackages: creating a named ~by-loader/python3Packages~ directory for loading the python packages.

** Example

- Loading Dir: [[https://github.com/GTrunSec/omnibus-examples/tree/main/packages/__fixture][omnibus-examples/packages/__fixture at main · GTrunSec/omnibus-examples]]

#+begin_src nix :tangle (concat (org-entry-get nil "EXP-DIR" t) "/packages/default.nix")
{ omnibus, inputs }:
let
  nixpkgs = inputs.nixpkgs.legacyPackages.x86_64-linux;
in
(omnibus.pops.packages {
  src = ./__fixture;
  inputs = {
    inputs = {
      inherit nixpkgs;
      source = import ./_sources/generated.nix {
        inherit (nixpkgs)
          fetchgit
          fetchurl
          fetchFromGitHub
          dockerTools
          ;
      };
    };
  };
})
# => out.exports { default = {...}, packages = {...}; }
#+end_src


#+begin_quote
Please note that in Pops, the outputs/results should not be directly printed, but instead, they will be exported through the exports property under the pop object.
#+end_quote


+ exports (layouts)
  - default (without *makeScope*)

#+begin_src nix
# => default
Flake.examples.x86_64-linux.packages.exports.default
{ by-loader = { ... }; hello1 = «derivation /nix/store/0xmz73mcmgx5c8q32b4jic63hgdw4bb2-example-unfree-package-2.0.drv»; hello2 = «der
ivation /nix/store/n08mpq35ycsxpg3pr73nfyywndr401ar-example-unfree-package-3.0.drv»; }
#+end_src

- packages: (with *makeScope*)

#+begin_src nix
Flake.examples.x86_64-linux.packages.exports.packages
{ __inputs__ = { ... }; by-loader = { ... }; callPackage = «lambda @ /nix/store/7b7m3p9gc9da6cd7wgyabs3wg1832j0v-source/lib/customisation.nix:153:31»; hello1 = «derivation /nix/store/0xmz73mcmgx5c8q32b4jic63hgdw4bb2-example-unfree-package-2.0.drv»; 
hello2 = «derivation /nix/store/n08mpq35ycsxpg3pr73nfyywndr401ar-example-unfree-package-3.0.drv»; newScope = «lambda @ /nix/store/7b7m3p9gc9da6cd7wgyabs3
wg1832j0v-source/lib/customisation.nix:319:22»; overrideScope = «lambda @ /nix/store/7b7m3p9gc9da6cd7wgyabs3wg1832j0v-source/lib/custo
misation.nix:321:27»; overrideScope' = «lambda @ /nix/store/7b7m3p9gc9da6cd7wgyabs3wg1832j0v-source/lib/customisation.nix:323:28»; pac
kages = «lambda @ /nix/store/7b7m3p9gc9da6cd7wgyabs3wg1832j0v-source/lib/fixed-points.nix:141:24»; }
#+end_src


- derivations: (flattenTreeDerivations without trivial attributes)

#+begin_src nix
Flake.examples.x86_64-linux.packages.exports.derivations
{ btest_1 = «derivation /nix/store/7yadv7z9yqzyvqwzd9wq6kwkn12jk958-python3.11-btest-1.1-test.drv»; btest_2 = «derivation /nix/store/i
rjy5jfahjgcsl6w1sc6913xdfdkkg2x-python3.11-btest-1.2-test.drv»; hello1 = «derivation /nix/store/0xmz73mcmgx5c8q32b4jic63hgdw4bb2-examp
le-unfree-package-2.0.drv»; hello2 = «derivation /nix/store/n08mpq35ycsxpg3pr73nfyywndr401ar-example-unfree-package-3.0.drv»; }
#+end_src


  - apply the overlay with nixpkgs
    #+begin_src nix :tangle (concat (org-entry-get nil "EXP-DIR" t) "/packages/packagesOverlay.nix")
{ super, omnibus }:
let
  nixos-23_11 = omnibus.flake.inputs.nixos-23_11;
in
nixos-23_11.legacyPackages.x86_64-linux.appendOverlays [
  super.packages.exports.overlays.composedPackages
  # super.packages.exports.overlays.default
  (final: prev: {
    python3Packages = prev.python3Packages.override (
      old: {
        overrides = prev.lib.composeExtensions (old.overrides or (_: _: { })) (
          selfP: _:
          # can be either super.packages.exports.packages.py.packages selfP
          {
            a = "1";
          }
        );
      }
    );
  })
]
    #+end_src
